# Copyright 1999-2012 Gentoo Foundation
# Distributed under the terms of the GNU General Public License v2
# $Header: $

EAPI=4
inherit libtool

DESCRIPTION="A complete ODBC driver manager"
HOMEPAGE="http://www.unixodbc.org/"
SRC_URI="http://ftp.unixodbc.org/pub/${PN}/${P}.tar.gz"

LICENSE="GPL-2 LGPL-2.1"
SLOT="0"
KEYWORDS="amd64"
IUSE="+minimal odbcmanual static-libs"

RDEPEND=">=sys-devel/libtool-2.2.6b
	>=sys-libs/readline-6.1
	>=sys-libs/ncurses-5.7-r7
	virtual/libiconv"
DEPEND="${RDEPEND}
	sys-devel/flex"

DOCS="AUTHORS ChangeLog NEWS README"

src_prepare() {
	elibtoolize
}

src_configure() {
	# --enable-driver-conf is --enable-driverc as per configure.in
	econf \
		--sysconfdir="${EPREFIX}"/etc/${PN} \
		$(use_enable static-libs static) \
		$(use_enable !minimal drivers) \
		$(use_enable !minimal driverc)
}

src_install() {
	default

	use prefix && dodoc README*
	use odbcmanual && dohtml -a css,gif,html,sql,vsd -r doc/*

	find "${ED}" -name '*.la' -exec sed -i -e "/^dependency_libs/s:=.*:='':" {} +
}
